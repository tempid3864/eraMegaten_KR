;「비전 퀘스트가 필요하신거죠？」

;2020/xx/xx										비전 퀘스트의 샘플로서 작성
;2020/08/19			kamedakeisuke(ﾟдﾟ)			有志가 만든 샘플이 오염되어버린 것 같아 차질이 없도록 조정
;												디버그 한정으로 「비전퀘스트 방식의 샘플이에요」이라고 명기한다
;2020/08/19			kamedakeisuke(ﾟдﾟ)			텐도마야가 이미 가입해있는 경우에는 발생시키지 않도록 수정（실행하면 사라지자너ー！）
;2022/09/19			kamedakeisuke				검격 부스터의 표기 오류 수정


;====================
;조건
;====================
@PLAYABLE_COLOSSEUM_25
;天堂真矢が居ない
IF 투기장플래그:25:0 == 0 && GETCHARA([[キャラ:마야（무대소녀）]]) < 0
	;DEBUG時のみ作動
	[IF_DEBUG]
		;RESULTS = Vision Quest:Star Divine
		RESULTS = 비전 퀘스트 샘플
		RETURN 1
	[ENDIF]
	RETURN 0
ENDIF
RETURN 0


;====================
;内容
;====================
@COLOSSEUM_25
PRINTFORMW 이것은 비전퀘스트입니다


;포지션1-6に存在する아군をすべてポイする
FOR LOCAL, 1, 7
	; LOCALSに포지션にいるキャラを取得
	LOCALS = 포지션{LOCAL}
	LOCAL:1 = FLAG:LOCALS
	
	; 誰もいなかったらCONTINUE
	SIF LOCAL:1 < 0
		CONTINUE
	
	; 포지션を外す（리턴同等処理）
	CALL REMOVE_POSITION, CFLAG:(LOCAL:1):포지션
	; 메시지の표시
	PRINTFORMW %CALLNAME:(LOCAL:1)%　가 파티에서 제외되었다
NEXT

;ゲストとして天堂真矢を配置する
CALL ADD_GUEST_COMPANION([[キャラ:마야（무대소녀）]], 0)
LOCAL = GETCHARA([[キャラ:마야（무대소녀）]])
;何かつけとかないと전투不能扱いになって即敗北する（天堂真矢が연모とか解釈違いなんですけど）
TALENT:LOCAL:연모 = 1

;任意の스테이터스にする
BASE:LOCAL:LV = 99
BASE:LOCAL:힘 = 80
BASE:LOCAL:지혜 = 65
BASE:LOCAL:마력 = 65
BASE:LOCAL:인내력 = 99
BASE:LOCAL:속도 = 50
BASE:LOCAL:운 = 65

;레벨だけ上げても스킬の습득は行われないので손動で설정する
ABL:LOCAL:스킬4 = [[스킬:도발]]
ABL:LOCAL:스킬5 = [[스킬:바람의춤]]
ABL:LOCAL:스킬6 = [[스킬:밤벚꽃의춤]]
ABL:LOCAL:스킬7 = [[스킬:데・카쟈]]
ABL:LOCAL:스킬8 = [[스킬:가이아브레이크]]
ABL:LOCAL:스킬9 = [[스킬:디아라한]]
ABL:LOCAL:스킬10 = [[스킬:간파]]
ABL:LOCAL:스킬11 = [[스킬:검격부스터]]
ABL:LOCAL:스킬12 = [[스킬:브레이브재퍼]]

;스테이터스を再計算し全회복
CALL SYNC_STATUS, LOCAL
CALL HEALTH_CHARA, LOCAL

PRINTFORMW ※이 이벤트는, 비전 퀘스트 실현을 위한 기술 샘플입니다. 디버그 모드 시에만 동작합니다
FORCEWAIT
PRINTFORML 
PRINTFORMW ──%CALLNAME:MASTER%는 꿈을 꾸고 있었다.
PRINTFORMW 꿈속에서 %CALLNAME:MASTER%는──무대소녀였다──
PRINTFORMW 나의 이름은, 그래──
PRINTFORML 
PRINTFORMW 『This is 텐도 마야』
PRINTFORML 
CUSTOMDRAWLINE =
PRINTL
PRINTW ──무대에 선 무대소녀를 스포트라이트가 비추어낸다.
PRINTL
SETCOLOR 0xfdca57
	PRINTFORML -다이바 나나-
RESETCOLOR
PRINTFORMW 「무대 위에 맺힌 탐스러운 과실.
PRINTFORMW 　하지만, 모두들 연약하니까, 내가 지켜주어야만 해
PRINTFORMW 　99기생, 다이바 나나
PRINTFORMW 　내가 지킬거야. 계속, 몇번이라도！」
PRINTFORML 
PRINTFORMW 무대소녀 다이바 나나 가 1체 나타났다！

CUSTOMDRAWLINE =
WAIT
FLAG:도주불가플래그 = 1
FLAG:회화불능플래그 = 1
FLAG:선제기습취소 = 1

CALL SET_ENEMY,9,[[キャラ:나나]],99,0,1
CFLAG:(CHARANUM-1):속도보정 += 20
CFLAG:(CHARANUM-1):ＨＰ보정 = (MAXBASE:(CHARANUM-1):ＨＰ / 2)
CFLAG:(CHARANUM-1):ＭＰ보정 += 5000
CFLAG:(CHARANUM-1):행동횟수 = 2
ABL:(CHARANUM-1):공격상성 = 0
ABL:(CHARANUM-1):스킬1 = [[스킬:데스바운드]]
ABL:(CHARANUM-1):스킬2 = [[스킬:악셀클로]]
ABL:(CHARANUM-1):스킬3 = [[스킬:일문자베기]]
ABL:(CHARANUM-1):스킬4 = [[스킬:묵시록]]
ABL:(CHARANUM-1):스킬5 = [[스킬:그라비티마인]]
ABL:(CHARANUM-1):스킬6 = [[스킬:이신의잔영]]
ABL:(CHARANUM-1):스킬7 = [[스킬:안개베기]]
ABL:(CHARANUM-1):스킬8 = [[스킬:트윈슬래시]]
ABL:(CHARANUM-1):스킬9 = 0
ABL:(CHARANUM-1):스킬10 = 0
ABL:(CHARANUM-1):스킬11 = 0
ABL:(CHARANUM-1):스킬12 = 0
CSTR:(CHARANUM-1):종족명 = 무대소녀
ABL:(CHARANUM-1):최저공격횟수 = 2
ABL:(CHARANUM-1):최대공격횟수 = 2
;CSTR:(CHARANUM-1):사고패턴 = 大場なな
;ABL:(CHARANUM-1):아이템1 = X
;ABL:(CHARANUM-1):입수확률1 = X
CALL SYNC_STATUS,(CHARANUM-1)
CALL HEALTH_CHARA,(CHARANUM-1)

CALL BATTLE_START
FLAG:도주불가플래그 = 0
FLAG:회화불능플래그 = 0
FLAG:선제기습취소 = 0

CUSTOMDRAWLINE =
WAIT

PRINTW ………
PRINTW ……
PRINTW …
PRINTL
PRINTFORML 
CALL MESSAGE_WINDOW_D, "기린", "레뷰 종료입니다"
PRINTW ＞이겼을텐데 느낌이 없다
PRINTW ＞정신을 차리고보니, 자신의 겉옷이 없어져 있었다
;天堂真矢をＰＴから外す
LOCAL = GETCHARA([[キャラ:마야（무대소녀）]])
CALL DEL_GUEST, LOCAL
PRINTL
PRINTW ──꿈의 시간은 끝났다. 당신의 의식은 급격하게 각성한다.

;당신を초기位置にセットする
CFLAG:MASTER:포지션 = 2
FLAG:포지션2 = 0
CFLAG:MASTER:PT플래그 = 2
CALL SYNC_STATUS, MASTER

투기장플래그:25:0 = 1

FOR LOCAL,0,CHARANUM
	CALL HEALTH_CHARA,LOCAL
NEXT

