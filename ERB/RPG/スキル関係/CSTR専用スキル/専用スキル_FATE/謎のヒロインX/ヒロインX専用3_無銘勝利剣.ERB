;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:無銘勝利剣.ERB
;	Facility	:
;
;	Licence		:ライセンスフリー
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	000		20XX/XX/XX		----					テンプレ
;	001		2021/04/28		しゃけ					作成
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;スキル名
@SKILL_NAME_無銘勝利剣,ARG
RESULTS = 무명승리검

;　명중
@SKILL_HITRATE_無銘勝利剣, ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"無銘勝利剣")

;1撃目以降で相手が回避失敗ではなかった場合、命中率を0に
SIF SKILLGAGE_H_GET(ARG,SKILL_NUM) > 1 && CFLAG:POS(CFLAG:ARG:타겟):회피실패 < 1
	RETURN 0
RETURN 130


;　위력
@SKILL_POWER_無銘勝利剣, ARG
#DIM SKILL_NUM
#DIM L_DMG
SKILL_NUM = PU_SKILLNUM_GET(ARG,"無銘勝利剣")
;総威力は300,450,500、強化イベント経由時は400,550,600
SELECTCASE SKILLGAGE_D_GET(ARG,PU_SKILLNUM_GET(ARG,"ノウブルㆍファンタズム"))
	CASE IS > 3
		SELECTCASE TOINT(CSTR:ARG:102)
			CASE IS >= 1
				L_DMG = 600
			CASEELSE
				L_DMG = 500
		ENDSELECT
	CASE IS > 1
		SELECTCASE TOINT(CSTR:ARG:102)
			CASE IS >= 1
				L_DMG = 550
			CASEELSE
				L_DMG = 450
		ENDSELECT
	CASEELSE
		SELECTCASE TOINT(CSTR:ARG:102)
			CASE IS >= 1
				L_DMG = 400
			CASEELSE
				L_DMG = 300
		ENDSELECT
ENDSELECT

SELECTCASE SKILLGAGE_H_GET(ARG,SKILL_NUM)
	CASE 1
		TIMES L_DMG , 0.05
	CASE 2
		TIMES L_DMG , 0.07
	CASE 3
		TIMES L_DMG , 0.05
	CASE 4
		TIMES L_DMG , 0.07
	CASE 5
		TIMES L_DMG , 0.05
	CASE 6
		TIMES L_DMG , 0.07
	CASE 7
		TIMES L_DMG , 0.05
	CASE 8
		TIMES L_DMG , 0.07
	CASE 9
		TIMES L_DMG , 0.05
	CASE 10
		TIMES L_DMG , 0.07
	CASE 11
		TIMES L_DMG , 0.05
	CASE 12
		TIMES L_DMG , 0.35
ENDSELECT

RETURN L_DMG

;クリティカル率
@SKILL_CRITICAL_RATE_無銘勝利剣, ARG
#DIM SKILL_NUM
SKILL_NUM = PU_SKILLNUM_GET(ARG,"無銘勝利剣")
;1撃目以降で相手が被クリティカルではなかった場合、クリティカル率を0に
IF SKILLGAGE_H_GET(ARG,SKILL_NUM) > 1
	CALL CRT_CHECK
	SIF !GETBIT(RESULT,2)
		RETURN 0
ENDIF
RETURN 30


;　최저공격횟수
@SKILL_MINATTACKNUMBER_無銘勝利剣,ARG
RETURN 1

;　최대공격횟수
@SKILL_MAXATTACKNUMBER_無銘勝利剣,ARG
RETURN 1


;　2 ＨＰ지불　3 ＭＰ지불
@SKILL_COSTTYPE_無銘勝利剣, ARG
RETURN 3
;消費なし＝1

;　코스트량
@SKILL_COST_無銘勝利剣,ARG
RETURN 40

;　상성
@SKILL_TYPE_無銘勝利剣,ARG
#LOCALSSIZE 5
#LOCALSIZE 4
SIF ARG < 0 || 스킬속성표시중
	RETURN GET_TYPE_NUM("검격")
SIF CFLAG:ARG:타겟 > 16 || CFLAG:ARG:타겟 < 1
	RETURN GET_TYPE_NUM("검격")
LOCAL = POS(CFLAG:ARG:타겟)
SIF LOCAL < 0
	RETURN GET_TYPE_NUM("검격")
	
LOCAL:2 = -300
;全部-400とかないとおもうけど、あったらあれなんで、剣撃をいれとく
LOCALS = 검격
LOCALS:1 = 파마
LOCALS:2 = 검격
LOCALS:3 = 주살
LOCALS:4 = 만능
FOR LOCAL:1, 1, 5
	;判定用に数値を仕舞う。反射は-200扱い
	LOCAL:3 = MAXBASE:LOCAL:(LOCALS:(LOCAL:1)) == 999 ? -200 # MAXBASE:LOCAL:(LOCALS:(LOCAL:1))
	LOCAL:3 = !(HAVE_SKILL(ARG,[[스킬:영세라이도우]]) || HAVE_SKILL(ARG,2947)) && CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"무효화횟수") ? -5 # LOCAL:3
	LOCAL:3 = CFLAG:LOCAL:(LOCALS:(LOCAL:1)+"가드킬") && LOCAL:3 < 100 ? 100 # LOCAL:3
	SELECTCASE LOCAL:1
		CASE 1
			LOCAL:3 = CFLAG:LOCAL:물리반사플래그 ? -200 # LOCAL:3
		CASE 2 TO 4
			LOCAL:3 = CFLAG:LOCAL:마법반사플래그 ? -200 # LOCAL:3
		CASEELSE
	ENDSELECT
	IF LOCAL:3 > LOCAL:2
		LOCAL:2 = LOCAL:3
		LOCALS = %LOCALS:(LOCAL:1)%
	ENDIF
NEXT
RETURN GET_TYPE_NUM(LOCALS)


;　1 EXTRA　2 MAGIC
@SKILL_DECIDE_TYPE_無銘勝利剣,ARG
RETURN 1

;　1 물리데미지로 계산　2 마법데미지로 계산
@SKILL_DAMAGETYPE_無銘勝利剣,ARG
RETURN 1


;　対象が1敵か2味方か3場全体か4特殊か
@SKILL_TARGET_無銘勝利剣,ARG
RETURN 1

;　사거리
@SKILL_RANGE_無銘勝利剣,ARG
RETURN 3

;　범위　1 1체　2 1열　3 전체　4 적아군전체
@SKILL_SPHERE_無銘勝利剣,ARG
RETURN 1

;　 효과타입　1 공격、2 회복、3 상태변화、4 특수、5 상태회복
@SKILL_EFECT_無銘勝利剣,ARG
RETURN 1

;　 특수처리 여부 및 처리내용
@SKILL_SPECIALEFECT_無銘勝利剣,ARG
;ARG = 1인 경우는 처리 여부를 반환합니다
SIF ARG == 1
	RETURN 0
;아래쪽에 특수처리를 기술합니다

;계승타입
;@SKILL_SUCCESSION_TYPE_無銘勝利剣,ARG
;RETURN 23
;검격

;속도보정
@SKILL_SPEED_無銘勝利剣, ARG = -1
RETURN 50

;;　口上の表示とそのタイプ
@SKILL_MESSAGE_無銘勝利剣,ARG
RETURN 0

;전투시 이외에 사용 가능한가
@SKILL_ACTIONABLE_FIELD_無銘勝利剣, ARG = -1
RETURN 0


;템플릿용、추가효과 시리즈
;추가효과ステータス
@SKILL_ADDTIONAL_STATE_無銘勝利剣,ARG
RETURN 0

;추가효과 상성
@SKILL_ADDTIONAL_TYPE_無銘勝利剣,ARG
RETURN 0

;추가효과基本付与確率
@SKILL_ADDTIONAL_BASE_RATE_無銘勝利剣,ARG
RETURN 0

;추가효과付与確率上限
@SKILL_ADDTIONAL_MAX_RATE_無銘勝利剣,ARG
RETURN 0


;====================================================
;トリガー発動条件
;====================================================
;@SKILL_TRIGGER_ACTIONABLE_無銘勝利剣,ARG,ARG:1
;RETURN 0


;====================================================
;ヒット時ダメージ前処理
;====================================================
;@SKILL_HIT_EFFECT_無銘勝利剣,ARG,ARG:1
;;ガードキル処理
;CFLAG:(ARG:1):검격무효화횟수 = 0
;CFLAG:(ARG:1):검격가드킬 = 1


;====================================================
;ダメージ発動時
;====================================================
;@SKILL_DAMAGE_EFFECT_無銘勝利剣,ARG,ARG:1

;====================================================
;行動によるスキルゲージ操作
;====================================================
;@SKILLGAGE_CHARGE_無銘勝利剣,ARG,ARG:1



;====================================================
;스킬 및 행동의 실행
;====================================================
@ACTION_無銘勝利剣,ARG,ARG:1,SKILL_NUM
#DIM SKILL_NUM
#DIM トドメまでお前を殴るのを止めないフラグ
#DIM HIT_COUNT



トドメまでお前を殴るのを止めないフラグ = 0
CFLAG:ARG:공격강화 += CFLAG:ARG:마법위력강화
;相手が元から不死身じゃないなら不死身にしてボコボコにしても問題ないな！
IF CFLAG:POS(ARG:1):불사신플래그 < 1
	CFLAG:POS(ARG:1):불사신플래그 = 1
	トドメまでお前を殴るのを止めないフラグ = 1
ENDIF
FOR HIT_COUNT,1,13
	;敵対時にスキル発動で口上を出す処理
	IF HIT_COUNT == 1 && CFLAG:ARG:PT플래그 == 0
		SETCOLOR 0xff0033
		CALL 구상호출 , "BATTLE_MESSAGE" , ARG , ARG
		RESETCOLOR
	ENDIF
	;6連撃目で口上を出す処理
	CALL SKILLGAGE_H_ADD,ARG,SKILL_NUM,1
	IF HIT_COUNT == 6
		SIF CFLAG:ARG:PT플래그 == 0
			SETCOLOR 0xff0033
		SIF CFLAG:ARG:PT플래그 == 2
			SETCOLOR 0x33ffcc 
		CALL 구상호출 , "BATTLE_MESSAGE" , ARG , ARG
		RESETCOLOR
	ENDIF
	;11連撃目で口上を出す処理
	IF HIT_COUNT == 11
		SIF CFLAG:ARG:PT플래그 == 0
			SETCOLOR 0xff0033
		SIF CFLAG:ARG:PT플래그 == 2
			SETCOLOR 0x33ffcc 
		CALL 구상호출 , "BATTLE_MESSAGE" , ARG , ARG
		RESETCOLOR
	ENDIF
	;トドメまでお前を殴るのを止めないフラグで付与された不死身フラグを解除してトドメを刺す
	IF HIT_COUNT == 12 && トドメまでお前を殴るのを止めないフラグ == 1
		SIF BASE:(POS(ARG:1)):ＨＰ < 1
			BASE:(POS(ARG:1)):ＨＰ = 1
		CFLAG:(POS(ARG:1)):불사신플래그 = 0
	ENDIF
	CALL ATTACK_SINGLE,ARG,POS(ARG:1),SKILL_NUM;	単体攻撃
	SIF HIT_COUNT == 12
		CALL SKILLGAGE_H_SET,ARG,SKILL_NUM,0
NEXT
CALL SKILLGAGE_D_SET,ARG,PU_SKILLNUM_GET(ARG,"ノウブルㆍファンタズム"),0
CFLAG:ARG:공격강화 -= CFLAG:ARG:마법위력강화

;====================================================
;실행내용 여기까지
;====================================================


;　説明文の表示
@SKILL_EXPLAIN_無銘勝利剣,ARG
PRINTL 1체 높은 명중률、높은 크리티컬률、세이버페이스 특공 검격、파마、주살、만능의 복합속성의 12연 소 데미지
PRINTL 
PRINTL 상반된 두 성검에 빔을 두른 후、빔 방출에 의한 제트 분사로 빠르게 접근한 후 적을 마구 베는 보구
PRINTL 휘두를 때마다 붕붕 하고 어딘가에서 들어본 듯한 소리를 내는、빛과 어둠이 어우러져 최강으로 보이는 왕도의 힘
PRINTL 이 스킬은 사용자의 마법위력강화의 영향을 받는다

;没
;@계승부위_無銘勝利剣,ARG

;사용조건
@SKILL_SPECIAL_ACTIONABLE_無銘勝利剣,ARG
SIF SKILLGAGE_D_GET(ARG,PU_SKILLNUM_GET(ARG,"ノウブルㆍファンタズム")) > 0
	RETURN 1
RETURN 0

;スキルゲージで役に立つ関数
;SKILLGAGE_H_GET(ARG,PU_SKILLNUM_GET(ARG,"無銘勝利剣"))
;CALL SKILLGAGE_H_SET,ARG,PU_SKILLNUM_GET(ARG,"無銘勝利剣"),0

;====================================================
;独自処理
;====================================================
;（予定）と付けられている方は仮置きの物。実際に採用されるかどうかはわからない。
;ARG = 使用者
;ARGS （ダメージ↑、被ダメージ↑、命中↑、回避↑、魔威↑、魔効↑、etc…）といった感じの奴
;ARG:1 = 対象
;文字列を参考にする都合上、どの文字列を使うか事前に定めておいた方がいいかも
@AUTO_PU_SKILL_無銘勝利剣,ARG,ARGS = "",ARG:1
#DIM SKILL_NUM
#DIM TOTAL_DMG_B
#DIM DYNAMIC L_SKILL_NAME
TOTAL_DMG_B = 0
SKILL_NUM = PU_SKILLNUM_GET(ARG,"無銘勝利剣")
SIF SKILL_NUM == -1
	RETURN
SELECTCASE ARGS
	CASE "ダメージ↑"
		IF CFLAG:ARG:입력행동 == SKILL_NUM
			CALL SABER_FACE_CHECK(ARG,ARG:1)
			IF RESULT == 1
				IF SKILLGAGE_H_GET(ARG,SKILL_NUM) == 12
					PRINTFORML 세이버페이스 특공！
					PRINTFORML %CALLNAME:ARG%의 가하는 데미지가 크게 상승！
					PRINTFORM 　　TARGET:[{CPOS(ARG:1),2}] %CALLNAME:(ARG:1),20,LEFT%　>>>>>>　
				ENDIF
				TOTAL_DMG_B += 150
			ENDIF
		ENDIF
		RETURN TOTAL_DMG_B
ENDSELECT

@SABER_FACE_CHECK,ARG,ARG:1
;セイバー顔として認識されるのはFGOでセイバー顔認定された悪魔を対象にする
;なお、別にTYPE-MOONキャラでなくても同名であれば対象の模様
SELECTCASE NO:(ARG:1)
	CASE [[キャラ:파랑세이버]],[[キャラ:빨강세이버]],[[キャラ:쟌느]],[[キャラ:쟌느ㆍ얼터]],[[キャラ:수수께끼의히로인X]],[[キャラ:수수께끼의히로인X얼터]]
		RETURN 1
ENDSELECT
RETURN 0

@HIT_CONTINUE_無銘勝利剣,ARG,ARG:1
#FUNCTION
#DIM SKILL_NUM

;一度でも宝具が命中した場合、以降の攻撃を確定命中にする処理
SKILL_NUM = PU_SKILLNUM_GET(ARG,"無銘勝利剣")
SIF CFLAG:ARG:입력행동 == SKILL_NUM && CFLAG:(ARG:1):회피실패 == 1
	RETURNF 1

@CRT_CONTINUE_無銘勝利剣,ARG,ARG:1
#DIM SKILL_NUM
;一度でも宝具がクリティカルした場合、以降の攻撃を確定クリティカルにする処理
;ARG:0　使用者
;ARG:1　対象者
SKILL_NUM = PU_SKILLNUM_GET(ARG,"無銘勝利剣")
;入力行動が無銘勝利剣であり、2段目の攻撃である必要がある
IF CFLAG:ARG:입력행동 == SKILL_NUM && SKILLGAGE_H_GET(ARG,SKILL_NUM) > 1
	;相手が被クリティカルを受けている
	IF GETBIT(CFLAG:(ARG:1):받음플래그,21)
		RETURN 1
	ELSE
		;こちらは防御状態でもクリティカルが発生した場合
		CALL CRT_CHECK
		SIF GETBIT(RESULT,2)
			RETURN 1
	ENDIF
ENDIF
RETURN 0